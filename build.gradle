plugins {
    id 'application'
    id 'java'
    id 'org.springframework.boot' version '3.0.4'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.vaadin' version '24.0.0'
}

group = 'org.rsinitsyn'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.vaadin.com/vaadin-addons' }
}

ext {
    set('vaadinVersion', "24.0.0")
    isProd = System.properties['env'] == 'prod'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'com.vaadin:vaadin-spring-boot-starter'
    implementation 'org.postgresql:postgresql'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // Monitoring
    implementation 'io.micrometer:micrometer-tracing'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // Addons Vaadin
    implementation 'com.github.umjammer:jlayer:1.0.2'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'de.jfancy:star-rating:1.0.3'
    implementation 'com.flowingcode.addons.carousel:carousel-addon:2.1.1'
    // Voice recognizer
    implementation 'org.vaadin.addons.pandalyte:voice-recognition:1.0.2'
    // Video player
//    implementation 'com.brownie.videojs:video-js-player:1.0.9'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
    }
}

vaadin {
    if (isProd) {
        print('Prod mode in gradle')
        productionMode = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
